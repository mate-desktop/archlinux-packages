# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-keyring
pkgver=1.6.0
pkgrel=2
pkgdesc="MATE Password Management daemon"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('dbus' 'gtk2' 'libtasn1' 'pam')
makedepends=('docbook-xml' 'mate-common' 'perl-xml-parser')
options=('!libtool' '!emptydirs')
groups=('mate')
provides=('gnome-keyring')
conflicts=('gnome-keyring')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('18b5a517b49f1df1d321bf684674af18e8f2d8df')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-pam-dir=/usr/lib/security \
        --with-root-certs=/etc/ssl/certs \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
