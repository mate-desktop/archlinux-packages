# Maintainer : György Balló <ballogy@freestart.hu>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=libindicator
pkgver=0.5.0
pkgrel=1
pkgdesc="A set of symbols and convience functions that all indicators would like to use"
url="https://launchpad.net/libindicator"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
makedepends=('gtk2' 'pkg-config')
options=(!libtool)
source=(https://launchpad.net/${pkgname}/0.5/${pkgver}/+download/${pkgname}-${pkgver}.tar.gz)
md5sums=('cab93d831eed5e48a97f4399ccaa4185')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    # Use different location for header files for GTK+3
    sed -i 's/libindicator-/libindicator3-/' libindicator/indicator3-0.4.pc.in.in
    sed -i 's/libindicator-/libindicator$(VER)-/' libindicator/Makefile.in

    # Disable building tests
    sed -i '/tests/ d' Makefile.in

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=2 \
        --disable-static
    make
}

package() {
  pkgdesc+=" (GTK+ 2 library)"
  depends=('gtk2')
  cd ${srcdir}/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}
