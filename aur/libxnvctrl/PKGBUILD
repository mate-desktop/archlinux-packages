# Maintainer  : Alessio Sergi <asergi at archlinux dot us>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=libxnvctrl
pkgver=325.15
pkgrel=2
pkgdesc="NVIDIA X Ctrl library"
arch=('i686' 'x86_64' 'armv6h' armv7h)
url="http://www.nvidia.com/"
license=('GPL2')
makedepends=('libxext')
source=(http://download.nvidia.com/XFree86/nvidia-settings/nvidia-settings-$pkgver.tar.bz2)
sha1sums=('9e043554225920decd16707c8a27a661b74d90d1')

build() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"
  make clean
  make
}

package() {
  cd "nvidia-settings-$pkgver/src/libXNVCtrl"
  # install libXNVCtrl and headers
  install -d -m 755 "$pkgdir"/usr/{include/NVCtrl,lib}
  install -m 644 nv_control.h NVCtrl{,Lib}.h "$pkgdir"/usr/include/NVCtrl/
  install -m 644 libXNVCtrl.a "$pkgdir"/usr/lib/libXNVCtrl.a
}

# vim:set ts=2 sw=2 et:
