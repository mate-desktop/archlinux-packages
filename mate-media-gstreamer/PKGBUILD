# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>
# Contributor: Piotr Gorski <sir_lucjan@bridgelinux.pl>

_pkgname=mate-settings-daemon
pkgname=${_pkgname}-gstreamer
pkgver=1.7.0
pkgrel=1
pkgdesc="The MATE Settings daemon (GStreamer)"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'dconf' 'fontconfig' 'gstreamer0.10-base-plugins' 'gtk2'
         'libcanberra' 'libcanberra-gstreamer' 'libmatekbd' 'libnotify'
         'libxt' 'mate-desktop' 'mate-polkit' 'startup-notification')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
conflicts=("${_pkgname}-pulseaudio" 'mate-media-pulseaudio')
provides=("${_pkgname}")
source=(http://pub.mate-desktop.org/releases/1.7/${_pkgname}-${pkgver}.tar.xz)
sha1sums=('32730c0c5ca20d41f11390a154875a1220d9ba9c')
install=${pkgname}.install

build() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${_pkgname} \
        --sysconfdir=/etc \
        --enable-polkit \
        --enable-gstreamer \
        --disable-pulse \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${_pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}

