#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-desktop
pkgver=1.6.0
pkgrel=1
pkgdesc="Library with common API for various MATE modules"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=(GPL LGPL)
depends=('libunique' 'libnotify' 'hicolor-icon-theme' 'gvfs-obexftp' 'obexd-client')
makedepends=('mate-doc-utils' 'intltool' 'mate-common' 'gtk-doc')
options=('!emptydirs' '!libtool')
url="http://mate-desktop.org"
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('d8c0dd7cc0cbd36088363bb1358e0acbf239c518e42504029a837cb993373d94')


build() {
    cd "$srcdir/$pkgname-$pkgver"

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --disable-scrollkeeper \
        --disable-startup-notification \
        --enable-unique || return 1
    # --disable-nyancat
    # --with-gtk=3.0
    make || return 1
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    make DESTDIR="$pkgdir" install

    # prevent conflicts errors
    mv "${pkgdir}/usr/share/omf" "${pkgdir}/usr/share/mate-omf"
}
