# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-applets
pkgver=1.6.1
pkgrel=4
pkgdesc="Applets for MATE panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://mate-desktop.org"
license=('GPL')
depends=('avahi' 'dbus' 'glib2' 'gsettings-desktop-schemas' 'gstreamer0.10-base-plugins'
         'gstreamer0.10-good-plugins' 'gtk-update-icon-cache' 'gtk2' 'gvfs'
         'libgtop' 'libmateweather' 'libmatewnck' 'libnotify' 'liboobs' 'libsoup'
         'libxklavier' 'libxml2' 'libxt' 'libx11' 'mate-desktop' 'mate-icon-theme'
         'mate-panel' 'mate-settings-daemon' 'polkit' 'pygtk' 'python2' 'python2-dbus'
         'python2-gobject2' 'python2-notify' 'sharutils' 'system-tools-backends'
         'upower' 'xproto')
makedepends=('docbook2x' 'docbook-xsl' 'mate-common' 'mate-doc-utils' 'networkmanager' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
replaces=('gnome-applets')
conflicts=('gnome-applets')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('520b064b45c9e0677ea5dea0f51698d3bc982371')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-polkit \
        --enable-networkmanager \
        --enable-ipv6 \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # Remove Python byte-code
    rm -rf ${pkgdir}/usr/lib/python2.*/site-packages/mate_invest/*.py{c,o}
    # Use python2
    sed -i 's/env python/env python2/' ${pkgdir}/usr/lib/${pkgname}/invest-applet

}
