# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-notification-daemon
pkgver=1.6.0
pkgrel=2
pkgdesc="Notification daemon for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL' 'LGPL')
depends=('dbus' 'desktop-file-utils' 'gtk2' 'libcanberra' 'libmatewnck' 'libnotify')
makedepends=('mate-common' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate')
provides=('notification-daemon')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('9988ac0d63ee8d2b7abd2ab47a8f57ad3d5c4c80')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --disable-static
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
