#Mantainer: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>
#Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-document-viewer
pkgver=1.6.1
pkgrel=2
pkgdesc="Simply a document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('gtk2' 'libspectre' 'gsfonts' 'poppler-glib' 'djvulibre' 'mate-icon-theme'
         't1lib' 'libmatekeyring' 'desktop-file-utils' 'libgxps') #  'dconf' 'gsettings-desktop-schemas'
makedepends=('mate-doc-utils' 'mate-file-manager' 'intltool'
             'gobject-introspection' 'gtk-doc' 'texlive-bin')
optdepends=('texlive-bin: DVI support')
groups=('mate-extras')
install=mate-document-viewer.install
options=('!libtool' '!emptydirs')
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('100feb278bb919de874e76bd2344cb49fa60e2819e98b1394226086e34bf59ce')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    # Work around a build system issue. This may not be required for 1.8
    autoreconf -v -i --force

    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --libexecdir=/usr/lib/mate-document-viewer \
        --disable-static \
        --enable-caja \
        --enable-pdf \
        --enable-tiff \
        --enable-djvu \
        --enable-dvi \
        --enable-t1lib \
        --enable-comics \
        --enable-pixbuf \
        --enable-xps \
        --disable-scrollkeeper \
        --enable-introspection \
        --disable-schemas-compile || return 1

    make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="$pkgdir/" install

    ln -s atril ${pkgdir}/usr/bin/mate-document-viewer

}
