# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-image-viewer
pkgver=1.6.1
pkgrel=2
pkgdesc="An image viewing and cataloging program for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('dbus' 'desktop-file-utils' 'gtk2' 'exempi' 'lcms' 'libexif'
         'libjpeg-turbo' 'mate-desktop' 'mate-icon-theme' 'pygtk' 'python2'
         'python2-gobject2' 'startup-notification' 'zlib')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
conflicts=('eog')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('b1f41a2c18d62e4573f6e6c9e4383c5030947808')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --localstatedir=/var \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
