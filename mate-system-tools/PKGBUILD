# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-system-tools
pkgver=1.6.0
pkgrel=5
pkgdesc="An extension for opening terminals in arbitrary local paths"
url="http://manny.cluecoder.org/packages/nautilus-open-terminal"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'gtk2' 'liboobs' 'mate-file-manager' 'mate-polkit'
         'system-tools-backends')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
optdepends=('ntp: Automatic time syncronisation.'
            'system-config-printer: Add CUPS printer configuration tool.'
            'cups-pk-helper: A helper that makes system-config-printer use PolicyKit.')
options=('!libtool')
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('89d53c22171f6181ad4fb89387d4fa3300bb27b2')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --disable-scrollkeeper \
        LIBS="-lm" # Required for MATE 1.6 but can be removed in MATE 1.7/1.8
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -f ${pkgdir}/usr/lib/caja/extensions-2.0/libcaja-gst-shares.a
}
