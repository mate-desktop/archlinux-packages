#Mantainer: Giovanni Ricciardi <kar98k.sniper@gmail.com>
#Contributor: Xpander <xpander0@gmail.com>

pkgname=mate-panel
pkgver=1.6.0
pkgrel=1
pkgdesc="The MATE Panel"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('libmatewnck' 'mate-menus' 'mate-desktop' 'librsvg' 'hicolor-icon-theme' 'libcanberra' 'libmateweather') # 'evolution-data-server'
makedepends=('mate-doc-utils' 'pkgconfig' 'intltool' 'networkmanager')
options=('!emptydirs' '!libtool')
install=mate-panel.install
source=(http://pub.mate-desktop.org/releases/1.6/$pkgname-$pkgver.tar.xz)
sha256sums=('41a0c30b7b2ddd3d4964c451d8d1ee12ba5fc62977232bd283ad9d28e217cafb')

url="http://mate-desktop.org"
groups=('mate')

build() {
    cd "${srcdir}/$pkgname-$pkgver"

    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --disable-static \
        --libexecdir=/usr/lib/mate-panel \
        --disable-scrollkeeper \
        --enable-introspection || return 1
       make || return 1
}

package() {
    cd "${srcdir}/$pkgname-$pkgver"

    make DESTDIR="${pkgdir}" install || return 1
}
