# Contributor: Giovanni Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-text-editor
pkgver=1.6.0
pkgrel=3
pkgdesc="A powerful text editor for MATE"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'enchant' 'glib2' 'gtk2' 'gtksourceview2' 'iso-codes'
         'libsm' 'libxml2' 'python2' 'python2-gobject2' 'pygtksourceview2')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('47d6ff5655c92baac70719247d22e7aabce56f4f')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --libexecdir=/usr/lib \
        --enable-python \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    # Remove Python byte-code
    rm -f ${pkgdir}/usr/lib/pluma/plugins/*/*.py{c,o}
}
