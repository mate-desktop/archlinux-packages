# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-disk-utility
pkgver=1.6.0
pkgrel=1
pkgdesc="Disk management application for MATE."
url="https://github.com/NiceandGently/mate-disk-utility"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('avahi' 'dbus' 'gtk2' 'gobject-introspection' 'libatasmart' 'libnotify' 'libunique'
         'mate-file-manager' 'mate-keyring' 'udisks')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!libtool' '!emptydirs')
groups=('mate-extras-community')
source=(https://github.com/NiceandGently/${pkgname}/archive/v${pkgver}.tar.gz)
md5sums=('48368fbf9b3ded23c82e772f8ef6f020')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}

    # Correct 'mate-keyring' version check
    sed -i 's/MATE_KEYRING_REQUIRED=2\.22/MATE_KEYRING_REQUIRED=1\.6\.0/' configure.ac

    ./autogen.sh \
        --prefix=/usr \
        --sysconfdir=/etc \
        --libexecdir=/usr/lib/${pkgname} \
        --enable-mate-keyring \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -f ${pkgdir}/usr/lib/*.a
}
