# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.9.0
pkgbase=mate-sensors-applet
pkgname=(${pkgbase}-gtk3)
pkgver=0
pkgrel=1
pkgdesc="A MATE Panel applet to display readings from hardware sensors, including CPU temperature, fan speeds and voltage readings. (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dbus' 'gtk3' 'libatasmart' 'libnotify' 'lm_sensors' 'mate-panel-gtk3>=1.9.4')
makedepends=('git' 'docbook-xml' 'libxnvctrl' 'mate-common' 'rarian' 'yelp-tools')
optdepends=('yelp: for reading MATE help documents')
groups=('mate-unstable-extra-gtk3')
conflicts=("${pkgbase}")
source=("${pkgbase}"::"git+https://github.com/mate-desktop/${pkgbase}.git")
sha1sums=('SKIP')
install=${pkgbase}.install

pkgver() {
    cd "${srcdir}/${pkgbase}"
    printf "%s.%s.%s" "${_ver}" "$(git log -1 --format=%cd --date=short | tr -d -)" "$(git rev-list --count HEAD)"
}

prepare() {
    cd "${srcdir}/${pkgbase}"
    NOCONFIGURE=1 ./autogen.sh
}

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgbase} \
        --with-gtk=3.0 \
        --disable-static
  make
}

package_mate-sensors-applet-gtk3() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
