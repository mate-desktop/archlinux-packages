# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-character-map
pkgver=1.6.0
pkgrel=3
pkgdesc="MATE Unicode Charmap"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('GPL')
depends=('desktop-file-utils' 'gobject-introspection' 'gtk2' 'pygtk' 'python2')
makedepends=('mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate-extras')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('e378e26ebe493d3f849771d10930504b8980448e')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    PYTHON=/usr/bin/python2 ./autogen.sh \
        --prefix=/usr \
        --enable-charmap \
        --enable-python-bindings \
        --enable-introspection \
        --disable-static \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
}
