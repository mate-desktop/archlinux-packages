# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-icon-theme
pkgver=1.6.2
pkgrel=2
pkgdesc="MATE icon theme"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('hicolor-icon-theme' 'gtk2' 'gtk-update-icon-cache' 'librsvg')
makedepends=('icon-naming-utils' 'mate-common' 'perl-xml-parser')
options=(!strip)
groups=('mate')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('28a7fb3236162d8154015bdd71da1e4e26a14412')
install=${pkgname}.install

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./configure \
        --prefix=/usr \
        --enable-test-themes
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    rm -f ${pkgdir}/usr/share/icons/mate/icon-theme.cache
}
