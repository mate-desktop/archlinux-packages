# Contributor: Giovanni "Talorno" Ricciardi <kar98k.sniper@gmail.com>
# Contributor: Xpander <xpander0@gmail.com>
# Contributor: Martin Wimpress <code@flexion.org>

pkgname=mate-dialogs
pkgver=1.6.0
pkgrel=2
pkgdesc="Display graphical dialog boxes from shell scripts"
url="http://mate-desktop.org"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('LGPL')
depends=('glib2' 'gtk2' 'libnotify')
makedepends=('docbook-xml' 'mate-common' 'mate-doc-utils' 'perl-xml-parser')
options=('!emptydirs' '!libtool')
groups=('mate')
conflicts=('zenity')
provides=('zenity')
replaces=('zenity')
source=(http://pub.mate-desktop.org/releases/1.6/${pkgname}-${pkgver}.tar.xz)
sha1sums=('68098c8bf6a888eebad99ba026794d93ae527e1a')

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    ./autogen.sh \
        --prefix=/usr \
        --disable-scrollkeeper
    make
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make DESTDIR="${pkgdir}" install
    ln -s matedialog ${pkgdir}/usr/bin/zenity
}
